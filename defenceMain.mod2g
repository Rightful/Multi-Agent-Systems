use Knowledge as knowledge.
use Actions as actionspec.
use PickupArmor as module.
use PickupHealth as module.
use PickupOther as module.
use PickupWeapon as module.
use DeliverFlag as module.
	
	
	% This is the main Module of the Defence bots. 
	% First it processes pickup before going to a location.

module defenceMain {

	% Pick up health items on the way.
	if bel(status(Health, _), Health < 90, (pickup(UnrealID, health, _), item(_, _, _, UnrealID)), not(navigation(noPath, _)), not(navigation(stuck, _))) then PickupHealth.
		
	% Pick up weapons on the way.
	if bel(pickup(UnrealID, weapon, _), item(_, weapon, NewWeapon, UnrealID), currentWeapon(CurrentWeapon, _), betterWeapon(NewWeapon, CurrentWeapon), not(weapon(NewWeapon, _, _)), not(navigation(noPath, _)), not(navigation(stuck, _))) then PickupWeapon.
		
	% Let the bot go to a module to pick up armor if needed.
	if bel(status(_, Armor), Armor < 80, item(_, armor, _, _)) then PickupArmor.
		
	% Pick up other items.
	if bel(item(_, _, _, UnrealID), pickup(UnrealID, other, O), not(O=invisibility), not(O=jump_boots), not(O=berserk), not(navigation(noPath, _)), not(navigation(stuck, _))) then PickupOther.
	
	% If our fellow attacker has the enemy flag, go to enemyBase.
	if bel(enemyBaseLocation(UnrealID), self(_,_,Team), not(holdingFlag), flagState(Team2, held), Team\=Team2, navigation(navigation, LOC), UnrealID\=LOC) then stop.
	if bel(enemyBaseLocation(UnrealID), self(_,_,Team), not(holdingFlag), flagState(Team2, held), Team\=Team2) then navigate(UnrealID).
	
	% If the defence bot has a goal to go a specific location the priority is checked and then the bot will handle this goal.
	if goal(goLocationFlag(Position)) then drop(goLocationFlag(Position)) + navigate(Position).		
	
	% If there is a goal to go home with the enemy flag, then drop that goal and go home.
	if goal(goHomeWithFlag(Position)) then drop(goHomeWithFlag(Position)) + navigate(Position).		
	
	% If there is a goal to go somewhere to help then drop that goal and go there.
	if goal(goLocationHelp(Position)) then drop(goLocationHelp(Position)) + navigate(Position).	
	
	% If there is a goal to go to the enemy Flag location then drop that goal and go there.
	if goal(goLocationFlagEnemy(Position)) then drop(goLocationFlagEnemy(Position)) + navigate(Position).	
	
	% If there is a goal to go to a random location then drop that goal and go to that random location.
	if goal(goRandom(Position)) then drop(goRandom(Position)) + navigate(Position).	
	
	% If there is a goal to go to a location to kill an enemy bot then drop that goal and go to that location.
	if goal(goLocationKill(Position)) then drop(goLocationKill(Position)) + navigate(Position).	
	
	% If there is a goal to go to a position then drop that goal and go to that position.
	if goal(goLocation(Position)) then drop(goLocation(Position)) + navigate(Position).
}