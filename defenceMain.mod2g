use Knowledge as knowledge.
use Actions as actionspec.
use PickupArmor as module.
use PickupHealth as module.
use PickupOther as module.
use PickupWeapon as module.
use DeliverFlag as module.

module defenceMain {

	% Pick up health on the way.
	if bel(status(Health, _), Health < 90, (pickup(UnrealID, health, _), item(_, _, _, UnrealID)), not(navigation(noPath, _)), not(navigation(stuck, _))) then PickupHealth.
		
	% Pick up weapons on the way.
	if bel(pickup(UnrealID, weapon, _), item(_, weapon, NewWeapon, UnrealID), currentWeapon(CurrentWeapon, _), betterWeapon(NewWeapon, CurrentWeapon), not(weapon(NewWeapon, _, _)), not(navigation(noPath, _)), not(navigation(stuck, _))) then PickupWeapon.
		
	% Let the bot go to a module to pick up armor if needed.
	if bel(status(_, Armor), Armor < 80, item(_, armor, _, _)) then PickupArmor.
		
	% Pick up other items.
	if bel(item(_, _, _, UnrealID), pickup(UnrealID, other, O), not(O=invisibility), not(O=jump_boots), not(O=berserk), not(navigation(noPath, _)), not(navigation(stuck, _))) then PickupOther.
	
	% If our fellow attacker has the flag, go to enemyBase.
	if bel(enemyBaseLocation(UnrealID), self(_,_,Team), not(holdingFlag), flagState(Team2, held), Team\=Team2, navigation(navigation, LOC), UnrealID\=LOC) then stop.
	if bel(enemyBaseLocation(UnrealID), self(_,_,Team), not(holdingFlag), flagState(Team2, held), Team\=Team2) then navigate(UnrealID).
	
	% If the defence bot has a goal to go a specific location the priority is checked and then the bot will handle this goal.
	if goal(goLocationFlag(Position)) then drop(goLocationFlag(Position)) + navigate(Position).		
	if goal(goHomeWithFlag(Position)) then drop(goHomeWithFlag(Position)) + navigate(Position).		
	if goal(goLocationHelp(Position)) then drop(goLocationHelp(Position)) + navigate(Position).	
	if goal(goLocationFlagEnemy(Position)) then drop(goLocationFlagEnemy(Position)) + navigate(Position).	
	if goal(goRandom(Position)) then drop(goRandom(Position)) + navigate(Position).	
	if goal(goLocationKill(Position)) then drop(goLocationKill(Position)) + navigate(Position).	
	if goal(goLocation(Position)) then drop(goLocation(Position)) + navigate(Position).
}